CHIP Memory {
    IN in[16], load, address[15];
    OUT out[16];

    PARTS:
	 // ---- address decoding ----
    Not(in=address[14], out=notA14);
    Not(in=address[13], out=notA13);

    // Load enables
    And(a=load, b=notA14,   out=ramLoad);     // address[14]==0  -> RAM16K
    And(a=load, b=address[14], out=a14);
    And(a=a14,  b=notA13,   out=screenLoad);  // address[14]==1 && address[13]==0 -> Screen
    // (Keyboardは書き込み不可なのでloadなし)

    // Devices
    RAM16K(in=in, load=ramLoad,   address=address[0..13], out=ramOut);
    Screen(in=in, load=screenLoad, address=address[0..12], out=screenOut);
    Keyboard(out=keyOut);

    // Output select:
    // if address[14]==0 -> RAM
    // else -> (address[13]==0 ? Screen : Keyboard)
    Mux16(a=screenOut, b=keyOut,  sel=address[13], out=hiOut);
    Mux16(a=ramOut,    b=hiOut,   sel=address[14], out=out);
}